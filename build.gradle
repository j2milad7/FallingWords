// Top-level build file where you can add configuration options common to all sub-projects/modules.

import dev.ma7.fallingwords.buildsrc.Libs
import dev.ma7.fallingwords.buildsrc.Versions
import io.gitlab.arturbosch.detekt.Detekt

buildscript {
    ext.gradleVersion = Versions.gradle
    ext.kotlinVersion = Versions.kotlin
    ext.hiltVersion = Versions.hilt
    ext.detektVersion = Versions.detekt

    dependencies {
        classpath Libs.Plugins.navigationSafeArgs
    }
}

plugins {
    id 'com.android.application' version "$gradleVersion" apply false
    id 'com.android.library' version "$gradleVersion" apply false
    id 'org.jetbrains.kotlin.android' version "$kotlinVersion" apply false
    id 'com.google.dagger.hilt.android' version "$hiltVersion" apply false
    id 'io.gitlab.arturbosch.detekt' version "$detektVersion" apply false
}

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"
    detekt {
        toolVersion = Versions.detekt
        config = files("${rootDir}/detekt.yml")
        parallel = true
        reports {
            xml.enabled = false
            txt.enabled = false
            html {
                enabled = true
                destination = file("${rootDir}/build/reports/detekt_${project.name}.html")
            }
        }
    }
    tasks.withType(Detekt) {
        exclude(".*/resources/.*")
        exclude(".*/build/.*")
    }
    dependencies {
        detektPlugins Libs.Plugins.detektFormatting
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}